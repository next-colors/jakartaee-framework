/*
 * (C) 2017 NEXT COLORS Co., Ltd.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
//-----------------------------------------------------------------------------
//    Plugins
//-----------------------------------------------------------------------------
plugins {
    id('java-library')
    id('jacoco')
    id('eclipse-wtp')
    alias(libs.plugins.gradle.dependency.graph.generator)
    alias(libs.plugins.gradle.dokka)
    alias(libs.plugins.gradle.lombok)
    alias(libs.plugins.gradle.versions)
}

//-----------------------------------------------------------------------------
//    Import Classes
//-----------------------------------------------------------------------------
import java.nio.charset.StandardCharsets
import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent
import io.franzbecker.gradle.lombok.task.DelombokTask
import org.jetbrains.dokka.gradle.DokkaTask
import org.jetbrains.dokka.DokkaConfiguration

//-----------------------------------------------------------------------------
//    Project Settings
//-----------------------------------------------------------------------------
group = 'jp.co.next-colors'

//-----------------------------------------------------------------------------
//    Constant Properties
//-----------------------------------------------------------------------------
// ビルド出力ディレクトリ
layout.buildDirectory = layout.projectDirectory.dir(getProperty('build.dir.output'))

//-----------------------------------------------------------------------------
//    Dependency Management
//-----------------------------------------------------------------------------
// コンフィギュレーションの設定
configurations {
    // テスト時にのみ必要なライブラリに、コンパイル時にのみ必要なライブラリを追加
    testImplementation.extendsFrom(compileOnly)
}

// 依存関係の設定
dependencies {
    // アノテーションプロセッサ
    annotationProcessor(libs.lombok)

    // パッケージに含めるライブラリ
    api(libs.classgraph)
    api(libs.commons.beanutils)
    api(libs.commons.codec)
    api(libs.commons.collections4)
    api(libs.commons.io)
    api(libs.commons.lang3)
    api(libs.generics.resolver)
    api(libs.jool)
    api(libs.jooq)
    api(libs.ognl)
    api(libs.slf4j.api)
    api(libs.toolfactory.jvm.driver)
    api(libs.uroborosql)

    // コンパイル時にのみ必要なライブラリ
    compileOnly(libs.jakartaee.api)
    compileOnly(libs.javax.faces)

    // テスト時にのみ必要なライブラリ
    testImplementation(libs.assertj.core)
    testImplementation(libs.assertj.db)
    testImplementation(libs.junit.jupiter.api)
    testImplementation(libs.mockito.inline)
    testImplementation(libs.mockito.junit.jupiter)
    testImplementation(libs.ninja.squad.dbsetup)

    // テスト実行時にのみ必要なライブラリ
    testRuntimeOnly(libs.junit.jupiter.engine)

    // Dokka のタスク実行時に必要なライブラリ
    // ※ Dokka で Java の書式でドキュメントを生成するためのライブラリ
    dokkaPlugin(libs.dokka.kotlin.as.java.plugin)
}

//-----------------------------------------------------------------------------
//    Plugin Configurations
//-----------------------------------------------------------------------------
// Java Plugin の設定
java {
    toolchain {
        // ビルド時に使用する Java のバージョン
        languageVersion = JavaLanguageVersion.of(JavaVersion.VERSION_17.majorVersion)
    }
}

// Gradle Lombok Plugin の設定
lombok {
    version = getProperty('lombok.version')
    sha256 = getProperty('lombok.checksum.sha256')
}

// Eclipse Plugin の設定
eclipse {
    // .project の設定
    project {
        // Gradle ネーチャーを追加
        natures('org.eclipse.buildship.core.gradleprojectnature')
        buildCommand('org.eclipse.buildship.core.gradleprojectbuilder')
    }

    // WTP（Web Tools Platform）の設定
    wtp {
        // プロジェクト・ファセットの設定
        facet {
            file {
                whenMerged {
                    // ファセットの種類、ファセット名でソート
                    facets.sort { facet1, facet2 ->
                        if (facet1.type == facet2.type) {
                            facet1.name <=> facet2.name
                        }
                        else {
                            facet2.type <=> facet1.type
                        }
                    }
                }
            }
        }
    }
}

//-----------------------------------------------------------------------------
//    Tasks
//-----------------------------------------------------------------------------
// JVM 言語でコンパイルするタスク
tasks.withType(AbstractCompile).configureEach {
    options.encoding = StandardCharsets.UTF_8
}

// Dokka のタスク
tasks.withType(DokkaTask).configureEach {
    dependsOn(tasks.delombok)

    dokkaSourceSets {
        main {
            sourceRoots.from(tasks.delombok.outputDir)

            documentedVisibilities.set([
                DokkaConfiguration.Visibility.PUBLIC,
                DokkaConfiguration.Visibility.PROTECTED
            ])

            jdkVersion.set(java.targetCompatibility.majorVersion.toInteger())
        }
    }

    pluginsMapConfiguration['org.jetbrains.dokka.base.DokkaBase'] = """\
    {
        "separateInheritedMembers": true,
        "footerMessage": "&#169; 2017-${Year.now()} NEXT COLORS Co., Ltd."
    }
    """.stripIndent()
}

// JAR ファイルを構築するタスク
tasks.jar {
    // マニフェストファイル
    manifest {
        attributes['Created-By'] = GradleVersion.current()
        attributes['Build-Jdk'] = System.getProperty('java.vm.version')
    }
}

// JUnit でテストするタスク
tasks.test {
    useJUnitPlatform()

    systemProperties['junit.jupiter.execution.parallel.enabled'] = true
    systemProperties['junit.jupiter.execution.parallel.mode.default'] = 'concurrent'

    ignoreFailures = true
    maxParallelForks = Math.max(Runtime.runtime.availableProcessors().intdiv(2), 1)

    testLogging {
        events(TestLogEvent.STARTED, TestLogEvent.PASSED, TestLogEvent.SKIPPED, TestLogEvent.FAILED)
        exceptionFormat = TestExceptionFormat.FULL
    }
}

// Jacoco でテストのレポートを生成するためのタスク
tasks.jacocoTestReport {
    dependsOn(tasks.test)
}

// Lombok による変換後のソースコードを生成するタスク
tasks.register('delombok', DelombokTask) {
    description = 'Generates delomboked sources.'

    group = lombok.NAME

    dependsOn(tasks.compileJava)

    ext.outputDir = layout.buildDirectory.dir(name).get()

    outputs.dir(outputDir)

    sourceSets.main.java.srcDirs.each { srcDir ->
        if (srcDir.exists()) {
            inputs.dir(srcDir)

            args(srcDir, '-d', outputDir)
        }
    }

    doFirst {
        delete(outputDir)
    }
}

// Lombok による変換後のソースコードを生成する際のヘルプを表示するタスク
tasks.register('delombokHelp', DelombokTask) {
    description = 'Displays the help about delomboking.'

    group = tasks.delombok.group

    args('--help')
}

// Lombok による変換後のソースコードを生成する際のフォーマットに関するヘルプを表示するタスク
tasks.register('delombokFormatHelp', DelombokTask) {
    description = 'Displays the help about the format of delomboking.'

    group = tasks.delombok.group

    args('--format-help')
}

// Javadoc のタスク
tasks.javadoc {
    dependsOn(tasks.delombok)

    source = tasks.delombok.outputDir
    failOnError = false

    options {
        encoding = StandardCharsets.UTF_8
        author = true
        use = true
        version = true
        bottom = "&#169; 2017-${Year.now()} NEXT COLORS Co., Ltd."
        links(
            'https://commons.apache.org/proper/commons-beanutils/apidocs/',
            'https://commons.apache.org/proper/commons-codec/apidocs/',
            'https://commons.apache.org/proper/commons-collections/apidocs/',
            'https://commons.apache.org/proper/commons-io/javadocs/api-release/',
            'https://commons.apache.org/proper/commons-lang/apidocs/',
            'https://docs.oracle.com/javase/jp/17/docs/api/',
            'https://jakarta.ee/specifications/platform/8/apidocs/',
            'https://javadoc.io/doc/io.github.classgraph/classgraph/',
            'https://javadoc.io/doc/ru.vyarus/generics-resolver/',
            'https://javadoc.io/doc/jp.co.future/uroborosql/',
            'https://projectlombok.org/api/',
            'https://www.jooq.org/javadoc/latest/',
            'https://www.jooq.org/products/jOO%CE%BB/javadoc/latest/',
            'https://www.slf4j.org/apidocs/'
        )
    }
}

// Eclipse プロジェクトのタスク
tasks.eclipse {
    doFirst {
        // Buildship の設定
        layout.projectDirectory.file('.settings/org.eclipse.buildship.core.prefs').asFile.withWriter { writer ->
            writer.println("connection.project.dir=${relativePath(rootDir)}")
            writer.println('eclipse.preferences.version=1')
        }
    }
}

// Gradle ラッパーのタスク
tasks.wrapper {
    gradleVersion = getProperty('gradle.version')
    distributionType = Wrapper.DistributionType.ALL
}
